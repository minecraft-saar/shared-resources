plugins {
    id 'java'
    // enables automatic version numbers from git tags
    // and provides additional gradle tasks to create releases (optional)
    id 'pl.allegro.tech.build.axion-release' version '1.10.1'
    id 'maven-publish'
}
group 'de.saar.minecraft'
description 'shared-resources'

// This defines releases as revisions
// that have a tag of the form "release-X.Y.Z"
scmVersion {
    ignoreUncommittedChanges = false
    tag {
        prefix = 'release'
    }
}
version = scmVersion.version

// define the github package registry as the maven repository we push our software to.
// this happens automatically thanks to the github actions settings.
publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/minecraft-saar/shared-resources")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("GPR_USER")
                password = project.findProperty("gpr.key") ?: System.getenv("GPR_API_KEY")
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            from(components.java)
        }
    }
}

// Local Variables:
// groovy-indent-offset: 4
// indent-tabs-mode: nil
// End:
